allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            project.android {
                if (namespace == null) {
                    namespace project.group
                }
                // Ensure consistent SDK levels across all modules/plugins
                compileSdkVersion 36
                ndkVersion = "27.0.12077973"
                defaultConfig {
                    targetSdkVersion 36
                    if (minSdkVersion == null || minSdkVersion.apiLevel < 23) {
                        minSdkVersion 23
                    }
                }
                // Ensure all modules compile Java at the same level
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }
            }
        }
    }
}

subprojects { project ->
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "17"
        }
    }
}

subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
    project.evaluationDependsOn(':app')

}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
